#!/usr/bin/env nu

let base = pwd
let lib = $base | path join lib

def day [y, d] {
	let dir = pwd | path join $"($y)" $"($d)"
	if not ($dir | path exists) {
		return 1
	}

	cd $dir

	if not ("input" | path exists) {
		aoc download -I -d $d -y $y
	}

	print -n $"Day ($d) of ($y) "
	for f in (glob main.* | append (glob a.*) | append (glob b.*)) {
		let p = $f | path parse
		let t = timeit { run-external $"($base)/test/run-($p.extension).sh" '.' $p.stem $lib }
		print ($t | format duration ms)
	}
}

def year [y] {
	for d in 1..25 {
		day $y $d
	}
}

def main [year?, day?] {
	if (which aoc | is-empty) {
		echo "aoc cli not found"
		exit 1
	}

	if $year != null and $day != null {
		day $year $day
	} else if $year != null {
		year $year
	} else {
		for y in (ls 20*) {
			year $y.name
		}
	}
}
